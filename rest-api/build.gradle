import groovy.json.JsonBuilder

import com.gmongo.GMongo
import com.mongodb.DB

import groovyx.net.http.HTTPBuilder
import static groovyx.net.http.ContentType.*
import static groovyx.net.http.Method.*

import com.aestasit.ssh.dsl.SshDslEngine
import com.aestasit.ssh.SshOptions

buildscript {
  repositories {
    maven { url "http://oss.jfrog.org/repo" }
    mavenCentral()
  }
  dependencies {
    classpath 'io.ratpack:ratpack-gradle:0.9.0-SNAPSHOT'
    classpath 'com.gmongo:gmongo:1.0'
    classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.6'
    classpath 'com.aestasit.infrastructure.sshoogr:sshoogr:0.9.3'
  }
}

project.ext {

  local = [
    server: 'localhost',
    database : 'localhost:27017',
    api : 'http://localhost:5050'
  ]

  production = [
    server: 'quiq.ly',
    database: 'quiq.ly:27017',
    api: 'http://quiq.ly:5050'
  ]

  questions = [
    [ 
      _id: UUID.randomUUID().toString(),
      _random: (Integer) (Math.random() * 100),
      title: 'Which of these languages can you use?', 
      answers: [
        [ title: 'Ruby' ],
        [ title: 'Python' ],
        [ title: 'Java' ],
        [ title: 'Groovy' ],
      ]
    ],
    [ 
      _id: UUID.randomUUID().toString(),
      _random: (Integer) (Math.random() * 100),
      title: 'What did you have dreams about last night?', 
      answers: [
        [ title: 'Female designers', image: '' ],
        [ title: 'Two pancakes', image: '' ],
        [ title: 'iPads', image: '' ],
        [ title: 'Miley Cyrus', image: '' ],
      ]
    ],
    /*[ 
      _id: UUID.randomUUID().toString(),
      _random: (Integer) (Math.random() * 100),
      title: 'Who will win Garage48 in Riga, 2013?', 
      answers: [
        [ title: 'Zentox' ],
        [ title: 'EduTree' ],
        [ title: 'Happinex' ],
        [ title: 'quiq.ly' ],
        [ title: 'BizCards' ],
        [ title: 'Feel Riga' ],
        [ title: 'Maps team' ],
        [ title: 'Collective praying' ],
        [ title: 'Freenlancer' ],
        [ title: 'Poble' ],
        [ title: 'myCogo' ],
        [ title: 'Remote Control' ],
        [ title: 'Best Friends Game' ],
        [ title: 'airbay.me' ],
        [ title: '3dcutting' ],
        [ title: 'Recipes' ],
      ]
    ],*/
  ]
}

repositories {
  maven { url "http://oss.jfrog.org/repo" }
  mavenCentral()
  maven { url "http://repo.springsource.org/repo" }
}

apply plugin: "ratpack-groovy"

configurations.all {
  exclude module: "groovy"
}

dependencies {
  springloaded 'org.springsource.springloaded:springloaded-core:1.1.4'
  compile 'com.gmongo:gmongo:1.0'
  compile 'org.codehaus.groovy:groovy-all:2.1.9'
}

test.onlyIf { false }

task cleanDb << {
  getDb().questions.drop()
  getDb().answers.drop()
}

task insertData << {
  questions.each { question ->
    def request = new JsonBuilder(question).toString()
    getDb().questions.insert(question)
  }
  getAnswerData().each { answer ->
    def request = new JsonBuilder(answer).toString()
    getDb().answers.insert(answer)
  }
}

insertData.mustRunAfter cleanDb

task insertHttpData << {
  questions.each { question ->
    def request = new JsonBuilder(question).toString()
    getHttp('api/questions/new').request(POST, JSON) { req ->
      body = request
      response.success = { resp, json ->
      }
    }
  }
}

task deploy(dependsOn: distZip) << {
  getSsh().remoteSession {
    exec(command: 'sudo killall java', failOnError: false)
    scp {
      from {
        localFile "${buildDir}/distributions/rest-api.zip"
      }
      into {
        remoteDir "/tmp"
      }
    }
    exec(command: 'sudo rm -rf /var/lib/tellme/*', failOnError: false)  
    exec(command: 'sudo mkdir -p /var/lib/tellme/storage', failOnError: false)
    exec 'sudo unzip /tmp/rest-api.zip -d /var/lib/tellme'
    exec 'sudo chown -R ubuntu:ubuntu /var/lib/tellme'
    exec 'cd /var/lib/tellme/rest-api/bin ; nohup sudo -b ./rest-api > /var/lib/tellme/rest-api/server.log'
  }
}


/////////////////////////////////////////////////////////////
// Utilities
/////////////////////////////////////////////////////////////

def getSsh() {
  def options = new SshOptions()
  options.with { 
    defaultKeyFile = new File('garage48.pem')
    defaultUser = 'ubuntu'
    defaultHost = project.production.server
    logger = new GradleLogger(project, true)
  }
  new SshDslEngine(options)  
}

def getDb() {
  if (project.hasProperty('env') && project.env == 'production') {
    def mongo = new GMongo(project.production.database)
    return mongo.getDB('tell-me')
  } else {
    def mongo = new GMongo()
    return mongo.getDB('tell-me')
  }
}

def getHttp(path) {
  if (project.hasProperty('env') && project.env == 'production') {
    return new HTTPBuilder("${project.production.api}/${path}")
  } else {
    return new HTTPBuilder("${project.local.api}/${path}")
  }
}

def getAnswerData() {
  def answers = []
  questions.each { question ->
    question.answers.each { answer ->
      int answerCount = (Integer) (Math.random() * 100)
      (1..answerCount).each {
        answers << [ 
          question_id: question._id,
          title: answer.title,
        ] 
      }
    }
  }
  answers
}


class GradleLogger implements com.aestasit.ssh.log.Logger {

  Project project
  boolean verbose

  GradleLogger(Project project, boolean verbose) {
    super()
    this.verbose = verbose
    this.project = project
  }

  def void debug(String message) {
    project.logger.debug(message)
  }

  def void info(String message) {
    if (verbose) {
      project.logger.quiet(message)
    } else {
      project.logger.info(message)
    }
  }

  def void warn(String message) {
    project.logger.warn(message)
  }
}
