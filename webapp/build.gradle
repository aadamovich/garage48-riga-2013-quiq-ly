import groovy.json.JsonBuilder

import com.gmongo.GMongo
import com.mongodb.DB

import groovyx.net.http.HTTPBuilder
import static groovyx.net.http.ContentType.*
import static groovyx.net.http.Method.*

import com.aestasit.ssh.dsl.SshDslEngine
import com.aestasit.ssh.SshOptions

buildscript {
  repositories {
    maven { url "http://oss.jfrog.org/repo" }
    mavenCentral()
  }
  dependencies {
    classpath 'io.ratpack:ratpack-gradle:0.9.0-SNAPSHOT'
    classpath 'com.gmongo:gmongo:1.0'
    classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.6'
    classpath 'com.aestasit.infrastructure.sshoogr:sshoogr:0.9.3'
  }
}

apply   from: 'envs.gradle'
apply   from: 'data.gradle'
apply plugin: 'ratpack-groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'

repositories {
  maven { url "http://oss.jfrog.org/repo" }
  mavenCentral()
  maven { url "http://repo.springsource.org/repo" }
}

configurations.all {
  exclude module: "groovy"
}

dependencies {
  springloaded 'org.springsource.springloaded:springloaded-core:1.1.4'
  compile 'com.gmongo:gmongo:1.0'
  compile 'org.codehaus.groovy:groovy-all:2.1.9'
  compile 'io.ratpack:ratpack-core:0.9.0-SNAPSHOT'
  compile 'io.ratpack:ratpack-groovy:0.9.0-SNAPSHOT'
}

test.onlyIf { false }

task cleanDb << {
  getDb().questions.drop()
  getDb().answers.drop()
}

task insertData << {
  questions.each { question ->
    def request = new JsonBuilder(question).toString()
    getDb().questions.insert(question)
  }
  getAnswerData().each { answer ->
    def request = new JsonBuilder(answer).toString()
    getDb().answers.insert(answer)
  }
}

insertData.mustRunAfter cleanDb

task insertHttpData << {
  questions.each { question ->
    def request = new JsonBuilder(question).toString()
    getHttp('api/questions/new').request(POST, JSON) { req ->
      body = request
      response.success = { resp, json ->
      }
    }
  }
}

task deploy(dependsOn: distZip) << {
  getSsh().remoteSession {
    exec(command: 'sudo killall java', failOnError: false)
    scp {
      from {
        localFile "${buildDir}/distributions/rest-api.zip"
      }
      into {
        remoteDir "/tmp"
      }
    }
    exec(command: 'sudo rm -rf /var/lib/quiq-ly/*', failOnError: false)  
    exec(command: 'sudo mkdir -p /var/lib/quiq-ly/storage', failOnError: false)
    exec 'sudo unzip /tmp/rest-api.zip -d /var/lib/quiq-ly'
    exec 'sudo chown -R ubuntu:ubuntu /var/lib/quiq-ly'
    exec 'cd /var/lib/quiq-ly/rest-api/bin ; nohup sudo -b ./rest-api > /var/lib/quiq-ly/rest-api/server.log'
  }
}


/////////////////////////////////////////////////////////////
// Utilities
/////////////////////////////////////////////////////////////

def getSsh() {
  def options = new SshOptions()
  options.with { 
    defaultKeyFile = new File('quiq-ly.pem')
    defaultUser = 'ubuntu'
    defaultHost = project.production.server
    logger = new GradleLogger(project, true)
  }
  new SshDslEngine(options)  
}

def getDb() {
  if (project.hasProperty('env') && project.env == 'production') {
    def mongo = new GMongo(project.production.database)
    return mongo.getDB('quiq-ly')
  } else {
    def mongo = new GMongo()
    return mongo.getDB('quiq-ly')
  }
}

def getHttp(path) {
  if (project.hasProperty('env') && project.env == 'production') {
    return new HTTPBuilder("${project.production.api}/${path}")
  } else {
    return new HTTPBuilder("${project.local.api}/${path}")
  }
}

def getAnswerData() {
  def answers = []
  questions.each { question ->
    if (question.title != 'Who will win Garage48 in Riga, 2013?') {
      question.answers.each { answer ->
        int answerCount = (Integer) (Math.random() * 100)
        (1..answerCount).each {
          answers << [ 
            question_id: question._id,
            title: answer.title,
          ] 
        }
      }
    }
  }
  answers
}


class GradleLogger implements com.aestasit.ssh.log.Logger {

  Project project
  boolean verbose

  GradleLogger(Project project, boolean verbose) {
    super()
    this.verbose = verbose
    this.project = project
  }

  def void debug(String message) {
    project.logger.debug(message)
  }

  def void info(String message) {
    if (verbose) {
      project.logger.quiet(message)
    } else {
      project.logger.info(message)
    }
  }

  def void warn(String message) {
    project.logger.warn(message)
  }
}
